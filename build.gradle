apply plugin: 'idea'


buildscript {
    repositories {
        mavenLocal()
        mavenRepo url: 'http://evgenyg.artifactoryonline.com/evgenyg/repo/'
    }
    dependencies {
        classpath 'com.github.goldin.plugins:teamcity:0.1.4-SNAPSHOT'
    }
}


ext.codenarcRuleSetFiles = [ 'codenarc.groovy' ]
ext.codenarcSources      = project.subprojects*.name.collect{ [ "$it/src/main/groovy", "$it/src/test/groovy" ] }.flatten()
apply from: 'https://raw.github.com/evgeny-goldin/gradle-plugins/master/src/main/groovy/CodeNarc.gradle'


idea.project {
    jdkName = '1.6'
    ipr.withXml { provider -> provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git' }
}


final springVersion     = '3.0.5.RELEASE'
final antVersion        = '1.8.4'
final gcontractsVersion = '1.2.5'
final spockVersion      = '0.6-groovy-1.8'
final junitVersion      = '4.10'
final teamCityVersion   = '7.0.4'


defaultTasks 'clean', 'codenarc', 'build', 'install'


subprojects {

    apply plugin: 'groovy'
    apply plugin: 'idea'
    apply plugin: 'maven'
    apply from:   "https://raw.github.com/evgeny-goldin/gradle-plugins/master/teamcity/scripts/teamcity-${teamCityVersion}.gradle"

    group                = 'com.github.goldin.teamcity'
    version              = '0.1-SNAPSHOT'
    sourceCompatibility  = 1.6
    targetCompatibility  = 1.6

    repositories   { mavenCentral() }

    configurations { compile { extendsFrom teamcityServer }}

    dependencies   {
        groovy      localGroovy()
        compile     ( "org.gcontracts:gcontracts-core:$gcontractsVersion" ) { exclude group: 'junit' }
        testCompile ( "org.spockframework:spock-core:$spockVersion" )       { exclude group: 'org.codehaus.groovy'  }
        testCompile "org.spockframework:spock-spring:$spockVersion",
                    "org.springframework:spring-test:$springVersion",
                    "org.apache.ant:ant:$antVersion",
                    "junit:junit:$junitVersion",
                    "log4j:log4j:1.2.17",
                    'com.google.guava:guava:13.0-rc2'
    }


    clean { delete( "$rootDir/out", "$rootDir/build" ) }


    idea.module {
        downloadSources = true
        downloadJavadoc = false
    }

    task sourcesJar( type: Jar, dependsOn: classes ) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
         archives sourcesJar
    }

    assembleTeamcityPluginConfig {
        serverConfigurations configurations.compile
        serverJars           jar
    }
}


project( ':messenger-plugin' ) {
    assembleTeamcityPluginConfig {
        displayName 'Messenger Plugin'
        description 'Plugin for sending messages between TeamCity users'
        vendorName  'Evgeny Goldin'
        vendorUrl   'http://evgeny-goldin.com'
    }
}


task wrapper( type: Wrapper ) { gradleVersion = '1.0' }
