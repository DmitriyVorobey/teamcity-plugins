
apply plugin: 'idea'


ext.codenarcRuleSetFiles = [ 'codenarc.groovy' ]
ext.codenarcSources      = project.subprojects*.name.collect{ [ "$it/src/main/groovy", "$it/src/test/groovy" ] }.flatten()
apply from: 'https://raw.github.com/evgeny-goldin/gradle-plugins/master/src/main/groovy/CodeNarc.gradle'


idea.project {
    jdkName = '1.6'
    ipr.withXml { provider -> provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git' }
}

final teamCityVersion   = '7.0.4'
final springVersion     = '3.0.5.RELEASE'
final antVersion        = '1.8.4'
final gcontractsVersion = '1.2.5'
final spockVersion      = '0.6-groovy-1.8'
final junitVersion      = '4.10'


defaultTasks 'clean', 'build'


subprojects {

    apply plugin: 'groovy'
    apply plugin: 'idea'
    apply from:   "file:$rootDir/teamcity-${teamCityVersion}.gradle"

    group                = 'com.github.goldin.teamcity'
    version              = '0.1-SNAPSHOT'
    sourceCompatibility  = 1.6
    targetCompatibility  = 1.6

    repositories { mavenCentral() }

    dependencies   {
        groovy      localGroovy()
        compile     ( "org.gcontracts:gcontracts-core:$gcontractsVersion" ) { exclude group: 'junit' }
        testCompile ( "org.spockframework:spock-core:$spockVersion" )       { exclude group: 'org.codehaus.groovy'  }
        testCompile "org.spockframework:spock-spring:$spockVersion",
                    "org.springframework:spring-test:$springVersion",
                    "org.apache.ant:ant:$antVersion",
                    "junit:junit:$junitVersion",
                    "log4j:log4j:1.2.17",
                    'com.google.guava:guava:13.0-rc2'
    }

    idea.module {
        downloadSources = true
        downloadJavadoc = true
    }
}

task wrapper( type: Wrapper ) { gradleVersion = '1.0' }
