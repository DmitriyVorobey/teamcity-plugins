apply plugin: 'idea'


buildscript {
    repositories {
        mavenLocal()
        mavenRepo url: 'http://evgenyg.artifactoryonline.com/evgenyg/repo/'
    }
    dependencies {
        classpath 'com.github.goldin.plugins:teamcity:0.1.4-SNAPSHOT'
    }
}


ext.codenarcRuleSetFiles = [ 'codenarc.groovy' ]
ext.codenarcSources      = project.subprojects*.name.collect{[ "$it/src/main/groovy", "$it/src/test/groovy" ]}.flatten()
apply from: 'https://raw.github.com/evgeny-goldin/gradle-plugins/master/src/main/groovy/CodeNarc.gradle'


idea.project {
    jdkName = '1.6'
    ipr.withXml { provider -> provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git' }
}


final springVersion     = '3.0.5.RELEASE'
final antVersion        = '1.8.4'
final gcontractsVersion = '1.2.5'
final spockVersion      = '0.6-groovy-1.8'
final junitVersion      = '4.10'


defaultTasks 'clean', 'codenarc', 'build', 'assembleTeamcityPlugin', 'install'


subprojects {

    Project p ->

    apply plugin: 'groovy'
    apply plugin: 'idea'
    apply plugin: 'maven'
    apply from: 'https://raw.github.com/evgeny-goldin/gradle-plugins/master/teamcity/teamcity.gradle'

    group                = 'com.github.goldin.teamcity'
    version              = '0.1-SNAPSHOT'
    sourceCompatibility  = 1.6
    targetCompatibility  = 1.6

    repositories   { mavenCentral() }

    configurations { compile { extendsFrom teamcityServer }}

    dependencies   {
        groovy      localGroovy()
        compile     ( "org.gcontracts:gcontracts-core:$gcontractsVersion" ) { exclude group: 'junit' }
        compile     'commons-logging:commons-logging-api:1.1'
        testCompile ( "org.spockframework:spock-core:$spockVersion" )       { exclude group: 'org.codehaus.groovy'  }
        testCompile "org.spockframework:spock-spring:$spockVersion",
                    "org.springframework:spring-test:$springVersion",
                    "org.apache.ant:ant:$antVersion",
                    "junit:junit:$junitVersion",
                    "log4j:log4j:1.2.17",
                    'com.google.guava:guava:13.0'
    }


    clean { delete( "$rootDir/out", "$rootDir/build" ) }


    idea.module {
        downloadSources = true
        downloadJavadoc = false
    }


    assembleTeamcityPlugin.dependsOn jar, test
    assembleTeamcityPluginConfig {
        vendorName   'Evgeny Goldin'
        vendorUrl    'http://evgeny-goldin.com'
        server       p
        artifacts    p
    }
}


project( ':messenger' ) {
    assembleTeamcityPluginConfig {
        displayName 'Messenger Plugin'
        description 'Plugin for sending messages between TeamCity users'
    }
}


project( ':github' ) {
    assembleTeamcityPluginConfig {
        displayName 'GitHub Plugin'
        description 'Plugin for integrating TeamCity with GitHub issue tracker'
    }
}


task wrapper( type: Wrapper ) { gradleVersion = '1.1' }
